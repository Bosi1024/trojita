# Definitions for using Ragel
# based on /usr/share/qt4/mkspecs/features/lex.prf
{
    ragelinclude.name = Copy Ragel includes around
    ragelinclude.input = RAGEL_INCLUDES
    ragelinclude.variable_out = GENERATED_SOURCES
    ragelinclude.commands = echo \"// dummy file\" > ${QMAKE_FILE_BASE}.ragel.included.file.cpp
    ragelinclude.output = ${QMAKE_FILE_BASE}.ragel.included.file.cpp
    ragelinclude.config += target_predeps
    QMAKE_EXTRA_COMPILERS += ragelinclude

    QMAKE_RAGELFLAGS = -C
    ragel.name = Ragel ${QMAKE_FILE_IN}
    ragel.input = RAGEL_SOURCES
    ragel.variable_out = GENERATED_SOURCES
    ragel.commands = ragel $$QMAKE_RAGELFLAGS ${QMAKE_FILE_IN} -o ${QMAKE_FILE_BASE}.ragel.cpp
    ragel.output = ${QMAKE_FILE_BASE}.ragel.cpp
    ragel.config += target_predeps

    QMAKE_EXTRA_COMPILERS += ragel

}

